plugins {
	id 'org.springframework.boot' version '2.6.6'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
}

subprojects {

	repositories {
		mavenCentral()
	}

	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'java'

	group = 'study.msa'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = '11'


	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}
	ext {
		// https://spring.io/projects/spring-cloud 의 Release Trains 부분을 보세요.
		set('springCloudVersion', "2021.0.1")
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}
	dependencies {
		annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
		developmentOnly("org.springframework.boot:spring-boot-devtools")

		implementation 'io.micrometer:micrometer-registry-prometheus'
		implementation 'org.springframework.boot:spring-boot-starter-actuator'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}

project(":resource-service") {
	dependencies {
		implementation 'org.springdoc:springdoc-openapi-ui:1.6.0'
		implementation 'org.springframework.boot:spring-boot-starter-web'

		implementation "org.springframework.cloud:spring-cloud-starter-openfeign"
		implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
		implementation 'org.springframework.cloud:spring-cloud-starter-config'

		annotationProcessor("org.projectlombok:lombok")
		compileOnly("org.projectlombok:lombok")

		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}
}

project(':spring-discovery-service') {

	dependencies {
		implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-server'
		implementation 'org.springframework.cloud:spring-cloud-starter-config'
	}
}

project (':spring-gateway-service') {
	dependencies {
		implementation "org.springframework.cloud:spring-cloud-starter-gateway"
		implementation 'org.springframework.cloud:spring-cloud-starter-config'
		implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
		implementation 'org.springdoc:springdoc-openapi-webflux-ui:1.5.13'
	}
}

project (':spring-config-service') {
	dependencies {
		implementation 'org.springframework.cloud:spring-cloud-config-server'
		implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	}
}
